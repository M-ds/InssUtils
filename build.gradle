plugins {
    id("java")
    id("jacoco")
    id("maven")
    id("signing")
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.3.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.3.1"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

// Test are required to run before generating the report
jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled(true)
    }
}

jacoco {
    toolVersion = "0.8.5"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

// Defined artifacts to be signed
signing {
    sign configurations.archives
}

group = "io.github.m-ds.inssutils"
version = "0.2.2"

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'InssUtils'
                packaging 'jar'
                description 'This repository has different utlity classes which can be used to either validate a INSS or generate one.'
                url 'https://github.com/M-ds/InssUtils'

                scm {
                    url 'https://github.com/M-ds/InssUtils'
                    connection 'scm:https://github.com/M-ds/InssUtils.git'
                    developerConnection 'scm:git://github.com/M-ds/InssUtils.git'
                }

                licenses {
                    license {
                        name 'MIT Licence'
                        url 'https://github.com/M-ds/InssUtils/blob/main/LICENSE'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'm-ds'
                        name 'Michiel de Smet'
                        email 'michieldesmet1994@gmail.com'
                    }
                }
            }
        }
    }
}
